plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version "1.8.5"
}

description "JLab Kafka Connect transform from epics2kafka to kafka-alarm-system"
group 'org.jlab.kafka.connect.transform'
version '0.13.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

repositories {
    jcenter()
}

dependencies {
    implementation group: 'org.apache.kafka', name: 'connect-transforms', version: '2.6.0'

    testImplementation 'junit:junit:4.12'
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "slominskir"
            name "Ryan Slominski"
            email "ryans@jlab.org"
        }
    }

    scm {
        url "https://github.com/JeffersonLab/kafka-transform-epics"
    }
}

publishing {
    publications {
        ProjectPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId project.name
            version version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', project.description)
                root.appendNode('name', project.name)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['ProjectPublication']

    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['MIT']
        vcsUrl = 'https://github.com/JeffersonLab/kafka-transform-epics.git'
        version {
            name = project.version
        }
    }
}